CODIGO->BLOQUE CODIGO'
CODIGO'->BLOQUE CODIGO'|ε
BLOQUE->INSTRUCCION BLOQUE'
BLOQUE'->salto INSTRUCCION BLOQUE'|ε
ANIDADO->INSTBLOQ salto ANIDADO'
ANIDADO'->INSTBLOQ salto ANIDADO'|ε
INSTBLOQ->ASIGNACION|OBTENER|PUBLICAR|CAMBIAR|BORRAR|COMENTARIO|CONDICIONAL
INSTRUCCION->DECLARACION|ASIGNACION|OBTENER|PUBLICAR|CAMBIAR|BORRAR|COMENTARIO|CONDICIONAL|SALTO
CONDICIONAL->si ESPACIO CONDICION ESPACIO entonces salto ANIDADO SINO finsi
SINO->sino salto ANIDADO|ε
CONDICION->COMPARACION CONDICION'
CONDICION'->&& COMPARACION CONDICION'|or COMPARACION CONDICION'|ε
COMPARACION->BOOL COMPARACION'
COMPARACION'->== BOOL COMPARACION'|< BOOL COMPARACION'|> BOOL COMPARACION'|ε
BOOL->identificador|constante|STRING
COMENTARIO-># STRING
OBTENER->obtener ESPACIO STRPARAM ESPACIO APIKEY
PUBLICAR->publicar ESPACIO STRPARAM ESPACIO STRPARAM ESPACIO APIKEY
CAMBIAR->cambiar ESPACIO STRPARAM ESPACIO DATO ESPACIO STRPARAM ESPACIO APIKEY
BORRAR->borrar ESPACIO STRPARAM ESPACIO DATO ESPACIO APIKEY
APIKEY->STRPARAM|ε
STRPARAM->STRING|identificador
DECLARACION->variable ESPACIO identificador ESPACIO = ESPACIO EXP
ASIGNACION->identificador ESPACIO = ESPACIO EXP
EXP->TERM EXP'
EXP'->ESPACIO OPSIMP ESPACIO TERM EXP'|ε
OPSIMP->+|-
TERM->FACTOR TERM'
TERM'->* FACTOR TERM'|/ FACTOR TERM'|ε
FACTOR->identificador|constante|STRING|( EXP )
ESPACIO->space ESPACIO|ε
SALTO->salto SALTO|ε
DATO->constante|STRING|identificador
STRING->' CADENA '
CADENA->∈Σ-' CADENA'
CADENA'->∈Σ-' CADENA'|ε